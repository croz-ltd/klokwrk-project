buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "com.bmuschko:gradle-clover-plugin:$gradleCloverPluginVersion"
    classpath "fr.brouillard.oss.gradle:gradle-jgitver-plugin:$gradleJgitverPluginVersion"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

// To enable "clean" task on the root project to be able to clean-up artifacts produced by "cloverAggregateReports" task
apply plugin: "base"

// To enable "cloverAggregateReports" on the root project
apply plugin: "com.bmuschko.clover"

apply plugin: "fr.brouillard.oss.gradle.jgitver"

jgitver {
  mavenLike true
  policy {
    pattern = "(.*)"
    transformations = ["IGNORE"]
  }
}

clover {
  report {
    html = true
    xml = true
  }
}

configure(subprojects) {
  group = "net.croz.cargo-tracker"
}

configure(subprojects.findAll({ !["cargo-tracker-platform"].contains(it.name) })) {
  repositories {
    mavenCentral()
  }

  apply plugin: "groovy"
  apply plugin: "maven-publish"
  apply plugin: "idea"
  apply plugin: "com.bmuschko.clover"

  java {
    sourceCompatibility = JavaVersion .VERSION_1_8
    targetCompatibility = JavaVersion .VERSION_1_8

    withSourcesJar()
  }

  test {
    jvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]
    useJUnitPlatform()
  }

  clover {
    // NOTE: cloverGenerateReport task executes all tests task in the project. Before doing so, it backs up previously compiled source code, then triggers compilation again with clover
    //       instrumentation included, executes tests and finally restores originally compiled classes.
    // NOTE: after changing following settings, task 'cloverGenerateReport' sometime requires to be run with '--rerun-tasks' option. Don't know what is root cause of this.
    enabled = true
    testIncludes = ["**/*Specification.groovy", "**/*Test.java", "**/*Test.groovy", "**/*Spec.groovy"]

    report {
      html = true
      xml = true
    }
  }

  tasks.register("groovyDocJar", Jar) {
    dependsOn groovydoc
    classifier "groovydoc"
    from groovydoc.destinationDir
  }

  publishing {
    publications {
      myLibrary(MavenPublication) {
        from components.java
        artifact groovyDocJar
      }
    }
  }

  // NOTE: This fixes IDEA configuration of Groovy projects and this is necessary for IDEA to recognize spring metadata files generated by Spring Boot annotation processing.
  //       - https://youtrack.jetbrains.com/issue/IDEA-215137
  //       - https://youtrack.jetbrains.com/issue/IDEA-211520
  idea {
    module {
      inheritOutputDirs = false
      outputDir = file("build/classes/groovy/main")
      testOutputDir = file("build/classes/groovy/test")
    }
  }

  dependencies {
    implementation platform(project(":cargo-tracker-platform"))
    annotationProcessor platform(project(":cargo-tracker-platform"))

    clover platform(project(":cargo-tracker-platform"))
    clover "org.openclover:clover"

    // NOTE - need to specify this one when Spock is used with Clover
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
  }
}
