buildscript {
  repositories {
    gradlePluginPortal()
    mavenCentral()
  }

  dependencies {
    classpath "fr.brouillard.oss.gradle:gradle-jgitver-plugin:$gradleJgitverPluginVersion"
    classpath "org.kordamp.gradle:codenarc-gradle-plugin:$gradleKordampPluginVersion"
    classpath "org.kordamp.gradle:groovydoc-gradle-plugin:$gradleKordampPluginVersion"
    classpath "org.kordamp.gradle:jacoco-gradle-plugin:$gradleKordampPluginVersion"
    classpath "org.kordamp.gradle:testing-gradle-plugin:$gradleKordampPluginVersion"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

repositories {
  mavenCentral()
  maven { url 'https://repo.spring.io/milestone' }
  maven { url 'https://jitpack.io' }
}

apply plugin: "fr.brouillard.oss.gradle.jgitver"
jgitver {
  mavenLike true
  policy {
    pattern = "(.*)"
    transformations = ["IGNORE"]
  }
}

apply plugin: "org.kordamp.gradle.codenarc"
apply plugin: "org.kordamp.gradle.groovydoc"
apply plugin: "org.kordamp.gradle.jacoco"
apply plugin: "org.kordamp.gradle.testing"
config {
  docs {
    groovydoc {
      // If one don't want to execute groovydoc as part of the assemble task, use gradle CLI task exclusion. For example: gw clean assemble -x groovydocJar
      // It also very useful to know which tasks exactly are part of task graph during execution. For that use: gw clean assemble -m
      setEnabled(true)
      setReplaceJavadoc(true)
      setIncludes(["**/*.groovy"] as Set)

      options {
        setUse(true)

        // Links to Groovy APIs are provided by plugin
        link(
            "https://docs.oracle.com/en/java/javase/11/docs/api/java.base",
            "java.io.", "java.lang.", "java.math.", "java.net.", "java.nio.", "java.security.", "java.text.", "java.time.", "java.util.", "javax.crypto.", "javax.net.", "javax.security."
        )
        link("https://docs.spring.io/spring-boot/docs/current/api/", "org.springframework.boot.")
        link("https://docs.spring.io/spring-framework/docs/current/javadoc-api", "org.aopalliance.", "org.springframework.")
        link("https://apidocs.axoniq.io/4.3/", "org.axonframework.", "io.axoniq.axonserver.grpc.")
      }
    }
  }

  quality {
    codenarc {
      setEnabled(true)
      setIgnoreFailures(true)
      setConfigFile(file("support/codenarc/codenarc.groovy"))
      setToolVersion("$codeNarcVersion")
    }
  }
}

// creates aggregateCodenarcMain and aggregateCodenarcTest tasks
void createAggregateCodenarcTasks() {
  List<String> codenarcSourceSetNameList = ["main", "test"]

  codenarcSourceSetNameList.each { String codenarcSourceSetName ->
    tasks.create(name: "aggregateCodenarc${codenarcSourceSetName.capitalize()}", type: CodeNarc) { CodeNarc codenarcTask ->

      codenarcTask.group = "Quality"
      codenarcTask.description = "Run aggregate CodeNarc analysis on all classes from '${codenarcSourceSetName}' source set."

      afterEvaluate {
        rootProject.pluginManager.apply(CodeNarcPlugin)

        Set<CodeNarc> subProjectCodenarcTaskList = new LinkedHashSet<>()
        rootProject.subprojects.each { Project subproject ->
          subproject.tasks.withType(CodeNarc) { CodeNarc task ->
            if (task.name == "codenarc${ codenarcSourceSetName.capitalize() }") {
              subProjectCodenarcTaskList << task
            }
          }
        }

        codenarcTask.ignoreFailures = false

        codenarcTask.source((subProjectCodenarcTaskList*.source).unique())
        codenarcTask.compilationClasspath = rootProject.files(((subProjectCodenarcTaskList*.compilationClasspath).unique()))
        codenarcTask.codenarcClasspath = rootProject.files(((subProjectCodenarcTaskList*.codenarcClasspath).unique()))

        codenarcTask.configFile = rootProject.file("support/codenarc/codenarc-${ codenarcSourceSetName }.groovy")
      }
    }
  }
}

createAggregateCodenarcTasks()

configure(subprojects) { Project subproject ->
  group = "org.klokwrk.project"

  configurations {
    all {
      resolutionStrategy {
        // prevent caching for snapshot dependencies
        //noinspection GroovyAssignabilityCheck
        cacheChangingModulesFor(0, "seconds")
      }
    }
  }

  if (["klokwrk-project-platform", "cargotracker", "lib", "lang"].contains(subproject.name)) {
    return
  }

  repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://jitpack.io' }
  }

  apply plugin: "groovy"
  apply plugin: "maven-publish"
  apply plugin: "idea"

  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
  }

  test {
    // TODO dmurat: remove this check when https://github.com/spockframework/spock/issues/1177 gets fixed.
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
      // NOTE: "-noverify" option must not be included for JDK 1.8. Otherwise, Spock's "thrown()" method results in IllegalAccessError.
      jvmArgs = ["-XX:TieredStopAtLevel=1"]
    }
    else {
      jvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]
    }

    useJUnitPlatform()
  }

  // When Groovy project contains Java files, and these Java files contain javadoc comments, javaparser-core needs to be added into groovydoc classpath (otherwise, groovydoc task might fail).
  // Do note that javaparser-core is a dependency of groovy-groovydoc, and its version should be updated when Groovy version changes.
  // Helpful documentation link: https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/GroovyRuntime.html
  Closure<FileCollection> createGroovyDocGroovyClasspath = { Project project ->
    Dependency javaParserDependency = project.getDependencies().create("com.github.javaparser:javaparser-core:$javaparserCoreVersion")
    FileCollection javaParserDependencyFileCollection = project.getConfigurations().detachedConfiguration(javaParserDependency)

    FileCollection groovyClasspath = project.groovyRuntime.inferGroovyClasspath(configurations.runtimeClasspath) + javaParserDependencyFileCollection
    return groovyClasspath
  }

  // Sometimes groovydoc needs additional dependencies in its classpath. With configuration bellow each subproject can configure additional groovydoc dependencies by using
  // groovydocExtendedClasspath. For example:
  //
  //   dependencies {
  //     ...
  //     groovydocExtendedClasspath "org.apache.tomcat.embed:tomcat-embed-core"
  //   }
  configurations {
    groovydocExtendedClasspath.extendsFrom(runtimeClasspath)
  }

  groovydoc {
    groovyClasspath = createGroovyDocGroovyClasspath(subproject) + subproject.configurations.groovydocExtendedClasspath
  }

  publishing {
    publications {
      myLibrary(MavenPublication) {
        from components.java
      }
    }
  }

  // NOTE: This fixes IDEA configuration of Groovy projects and this is necessary for IDEA to recognize spring metadata files generated by Spring Boot annotation processing.
  //       - https://youtrack.jetbrains.com/issue/IDEA-215137
  //       - https://youtrack.jetbrains.com/issue/IDEA-211520
  idea {
    module {
      inheritOutputDirs = false
      outputDir = file("build/classes/groovy/main")
      testOutputDir = file("build/classes/groovy/test")
    }
  }

  dependencies {
    annotationProcessor platform(project(":klokwrk-project-platform"))
    implementation platform(project(":klokwrk-project-platform"))
    codenarc platform(project(":klokwrk-project-platform"))

    codenarc "org.codenarc:CodeNarc"
    codenarc "org.codehaus.groovy:groovy"
    codenarc "org.codehaus.groovy:groovy-templates"
  }
}
