/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2022 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "java-platform"
  id "klokwrk-gradle-plugin-convention-module-base"
}

description = "Micronaut related platform for klokwrk-project modules that need Micronaut features or dependencies managed by Micronaut."

javaPlatform {
  allowDependencies()
}

config {
  info {
    setName(project.name)
    setDescription(project.description)
  }

  publishing {
    setPublications(["klokwrkPlatformMicronaut"])
    pom {
      setPackaging("pom")
    }
  }
}

publishing {
  publications {
    //noinspection GroovyAssignabilityCheck
    klokwrkPlatformMicronaut(MavenPublication) {
      //noinspection GroovyAssignabilityCheck
      from components.javaPlatform
    }
  }
}

dependencies {
  api platform(project(":klokwrk-platform-base"))
  api platform("io.micronaut:micronaut-bom:$micronautVersion")

  //Note: WireMock:2.34.x (2.34.0 at the time of writing) depends on slf4j:2.x (2.0.1 at the time of writing) that introduces a new mechanism for resolving slf4j bindings.
  //      Since Micronaut still relies on the older (1.7.x) slf4j binding mechanics, we are constraining the version of slf4j dependencies.
  api("org.slf4j:slf4j-api") {
    version {
      strictly "$micronautSlf4jVersion"
    }
  }

  constraints {
    api "net.bytebuddy:byte-buddy:$byteBuddyVersion"
  }
}
