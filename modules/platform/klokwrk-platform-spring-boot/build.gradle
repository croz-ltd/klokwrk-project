/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2022 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "java-platform"
  id "klokwrk-gradle-plugin-convention-base"
}

description = "Spring Boot related platform for klokwrk-project modules that need Spring Boot features or dependencies managed by Spring Boot."

javaPlatform {
  allowDependencies()
}

config {
  info {
    setName(project.name)
    setDescription(project.description)
  }

  publishing {
    setPublications(["klokwrkPlatformSpringBoot"])
    pom {
      setPackaging("pom")
    }
  }
}

publishing {
  publications {
    //noinspection GroovyAssignabilityCheck
    klokwrkPlatformSpringBoot(MavenPublication) {
      //noinspection GroovyAssignabilityCheck
      from components.javaPlatform
    }
  }
}

dependencies {
  api platform(project(":klokwrk-platform-base"))
  api platform("com.wavefront:wavefront-spring-boot-bom:$wavefrontSpringBootVersion")
  api platform("io.opentracing.contrib:opentracing-spring-cloud-starter:$openTracingSpringCloudVersion")
  api platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

  api "io.opentracing:opentracing-api:$openTracingApiVersion"

  // Note: ArchUnit:1.x (1.0.0 at the time of writing) depends on slf4j:2.x (2.0.3 at the time of writing), but that version does not work with Spring Boot apps yet.
  //       Therefore, we are constraining slf4j version to the one used by Spring Boot BOM. When Spring Boot is updated, this version range should be checked too.
  //       After upgrading to Spring Boot 3.x, this version and related Gradle constraint should be removed.
  // TODO dmurat: remove after upgrading to Spring Boot 3
  api("org.slf4j:slf4j-api") {
    version {
      strictly "$springBootSlf4jVersion"
    }
  }
}
