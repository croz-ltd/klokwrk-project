/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2021 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "org.springframework.boot"
apply plugin: "klokwrk-gradle-plugin-convention-test-integration"

// https://docs.gradle.org/current/userguide/upgrading_version_6.html#publishing_spring_boot_applications
configurations {
  [apiElements, runtimeElements].each {
    it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
    it.outgoing.artifact(bootJar)
  }
}

springBoot {
  // Generates META-INF/build-info.properties, which contains (beside other things) version generated by jgitver gradle plugin. That version (and other data from build-info.properties) are than
  // available via BuildProperties bean at runtime.
  buildInfo {
    properties {
      // Update build time only when "assemble" task is in the graph (https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#integrating-with-actuator-build-info).
      // Note that the build still is not reproducible with this change, but the execution of separate tasks after "assemble" will not update build time anymore. This way, we can, for example,
      // execute tests and JaCoCo report generation separately without JaCoCo triggering test execution again.
      gradle.taskGraph.whenReady { taskGraph ->
        if (!taskGraph.hasTask(":assemble")) {
          time = null
        }
      }
    }
  }
}

bootBuildImage {
  enabled = true
  environment = ["BP_JVM_VERSION" : "11.*"]
  imageName = "klokwrkprj/${project.name}:${project.version}"
}

dependencies {
  implementation platform(project(":klokwrk-platform-spring-boot"))
  testIntegrationImplementation platform("org.testcontainers:testcontainers-bom:$testContainersVersion")

  implementation project(":cargotracker-booking-axon-api")
  implementation project(":cargotracker-booking-boundary-web")
  implementation project(":cargotracker-booking-domain-model")
  implementation project(":cargotracker-lib-axon-api")
  implementation project(":cargotracker-lib-axon-logging")
  implementation project(":cargotracker-lib-axon-cqrs")
  implementation project(":cargotracker-lib-boundary-api")
  implementation project(":cargotracker-lib-web")
  implementation project(":klokwrk-lib-jackson-springboot")
  implementation project(":klokwrk-lib-validation-constraint")
  implementation project(":klokwrk-lib-validation-springboot")
  implementation project(":klokwrk-lang-groovy")
  implementation project(":klokwrk-lang-groovy-contracts-match")
  implementation project(":klokwrk-lang-groovy-contracts-simple")

  implementation "org.axonframework:axon-spring-boot-starter"
  implementation "org.codehaus.groovy:groovy"
  implementation "org.codehaus.groovy:groovy-macro"
  implementation "org.hamcrest:hamcrest"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation "org.springframework.boot:spring-boot-starter-web"

  runtimeOnly "com.wavefront:wavefront-spring-boot-starter"
  runtimeOnly "io.opentracing.contrib:opentracing-spring-cloud-starter"
  runtimeOnly "org.axonframework.extensions.tracing:axon-tracing-spring-boot-starter"

  testImplementation project(":cargotracker-booking-commandside-test-support")
  testImplementation project(":klokwrk-lib-archunit")

  testImplementation "com.tngtech.archunit:archunit-junit5"
  testImplementation "org.axonframework:axon-test"
  testImplementation "org.spockframework:spock-core"

  testIntegrationImplementation "org.springframework:spring-test"
  testIntegrationImplementation "org.springframework.boot:spring-boot-test"
  testIntegrationImplementation "org.testcontainers:testcontainers"

  testIntegrationRuntimeOnly "org.spockframework:spock-spring"
}
