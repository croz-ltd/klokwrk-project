/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2022 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "org.springframework.boot"
}

config {
  publishing {
    setPublications(["groovyModule", "springBootAppModule"])
  }
}

publishing {
  publications {
    springBootAppModule(MavenPublication) {
      artifact bootJar
    }
  }
}

springBoot {
  // Generates META-INF/build-info.properties, which contains (beside other things) version generated by jgitver gradle plugin. That version (and other data from build-info.properties) are than
  // available via BuildProperties bean at runtime.
  buildInfo {
    properties {
      // Update build time only when "assemble" task is in the graph (https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#integrating-with-actuator-build-info).
      // Note that the build still is not reproducible with this change, but the execution of separate tasks after "assemble" will not update build time anymore. This way, we can, for example,
      // execute tests and JaCoCo report generation separately without JaCoCo triggering test execution again.
      gradle.taskGraph.whenReady { taskGraph ->
        if (!taskGraph.hasTask(":assemble")) {
          time = null
        }
      }
    }
  }
}

bootBuildImage {
  enabled = true
  environment = ["BP_JVM_VERSION" : "11.*"]
  imageName = "klokwrkprj/${project.name}:${project.version}"
  buildpacks = ["gcr.io/paketo-buildpacks/adoptium", "urn:cnb:builder:paketo-buildpacks/java"]
}
