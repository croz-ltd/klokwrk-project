/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2021 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "org.gradle.test-retry"
  id "org.klokwrk.gradle.test-retry-disabler"
}

// =================================================
// configuring "testComponent" source set -- start
// -------------------------------------------------
sourceSets {
  testComponent {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output

    java {
      outputDir = file("build/classes/groovy/testComponent")
      srcDirs = [file("src/testComponent/groovy")]
    }

    resources {
      srcDirs = [file("src/testComponent/resources")]
    }
  }
}

configurations {
  testComponentImplementation.extendsFrom testImplementation
  testComponentRuntimeOnly.extendsFrom testRuntimeOnly
}

/**
 * Converts Gradle project properties (i.e. properties specified via gradle.properties file) with 'DockerImageVersion' suffix into Java system properties.
 * This way all versions with DockerImageVersion suffix can be used by Testcontainers factories that create and start containers.
 */
void convertDockerImageVersionsIntoJavaSystemPropertiesForTestTask(Test testTask, Project project) {
  Map<String, String> dockerImageVersions = project.getRootProject().properties.findAll { String key, Object value ->
    (value instanceof String) && key.toLowerCase().endsWith("DockerImageVersion".toLowerCase())
  } as Map<String, String>

  dockerImageVersions.each { String key, String value ->
    testTask.systemProperty(key, value)
  }
}

tasks.register("testComponent", Test) { Test testComponentTask ->
  description = "Runs component tests."
  group = "verification"

  convertDockerImageVersionsIntoJavaSystemPropertiesForTestTask(testComponentTask, project)
  testComponentTask.useJUnitPlatform()

  testClassesDirs = sourceSets.testComponent.output.classesDirs
  classpath = sourceSets.testComponent.runtimeClasspath

  jvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]

  // Docker based tests in CI environments (like GitHub Actions) may be flaky. Therefore, we will retry 2 times (3 test execution in total) before giving up.
  retry {
    maxRetries = 2
  }
}

idea {
  module {
    // Required for IDEA to mark sources from testComponent source set as test sources.
    testSourceDirs += file("src/testComponent/groovy")
    testResourceDirs += file("src/testComponent/resources")
  }
}
// -----------------------------------------------
// configuring "testComponent" source set -- end
// ===============================================

