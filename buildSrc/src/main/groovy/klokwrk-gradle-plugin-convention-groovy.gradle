/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2022 CROZ d.o.o, the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "groovy"
  id "idea"
  id "klokwrk-gradle-plugin-convention-base"
  id "org.kordamp.gradle.publishing"
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

test {
  jvmArgs = ["-XX:TieredStopAtLevel=1"]
  useJUnitPlatform()
}

configurations {
  groovydocCustomClasspath
}

groovydoc {
  groovyClasspath = project.configurations.groovydocCustomClasspath
}

config {
  info {
    setName(project.name)
    setDescription(project.description == rootProject.description ? project.name : project.description)
  }

  publishing {
    setPublications(["groovyModule"])
  }
}

publishing {
  publications {
    groovyModule(MavenPublication) {
      from components.java
    }
  }
}

// NOTE: This fixes IDEA configuration of Groovy projects and this is necessary for IDEA to recognize spring metadata files generated by Spring Boot annotation processing.
//       - https://youtrack.jetbrains.com/issue/IDEA-215137
//       - https://youtrack.jetbrains.com/issue/IDEA-211520
idea {
  module {
    inheritOutputDirs = false
    outputDir = file("build/classes/groovy/main")
    testOutputDir = file("build/classes/groovy/test")
  }
}

dependencies {
  codenarc platform(project(":klokwrk-platform-base"))
  groovydocCustomClasspath platform(project(":klokwrk-platform-base"))

  codenarc "org.codenarc:CodeNarc"
//  codenarc "com.github.codenarc:codenarc"

  groovydocCustomClasspath "com.github.javaparser:javaparser-core"
  groovydocCustomClasspath "org.apache.groovy:groovy"
  groovydocCustomClasspath "org.apache.groovy:groovy-ant"
  groovydocCustomClasspath "org.apache.groovy:groovy-templates"
}
